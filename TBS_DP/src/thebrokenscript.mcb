function load minecraft:load{
    scoreboard objectives add mcb.internal dummy
    scoreboard objectives add tbs.event_temp dummy
    scoreboard objectives add tbs.moon_event dummy
    scoreboard objectives add tbs.entity_temp dummy
    scoreboard objectives remove tbs.overlay_lib
    scoreboard objectives add tbs.overlay_temp dummy
    execute unless score enabled tbs.moon_event = enabled tbs.moon_event run scoreboard players set enabled tbs.moon_event 0
    scoreboard players set 8 tbs.moon_event 8
}

function tick minecraft:tick {
    execute as @e[tag=tbs.event.4.fire] at @s run {
        scoreboard players add @s tbs.event_temp 1
        execute if score @s tbs.event_temp matches 25.. run fill ~ ~ ~ ~ ~ ~ air replace fire
        execute if score @s tbs.event_temp matches 25.. run kill
    }
    function thebrokenscript:night_lib/main
    function thebrokenscript:overlay_lib/as_player
    function thebrokenscript:entity/player
    execute as @e[tag=tbs.entity] at @s rotated as @s run function thebrokenscript:entity/entity_tick
}

function damage_macro {
    $execute at @s run summon marker ~ ~ ~ {CustomName:'"$(name)"',Tags:["tbs.damage"]}
    $damage @s $(damage) minecraft:mob_attack by @n[tag=tbs.damage]
    kill @n[tag=tbs.damage]
}

function rotation_treshold {
    # d = mod(abs (t1 - t2),360)
    # 
    # if (d > 180)
    #   d = 360 - d
    #
    # if (d <= 10)
    #
    # inputs:
    # current
    # desired
    # threshold

    # scoreboard objectives remove tbs.threshold_temp
    scoreboard objectives add tbs.threshold_temp dummy
    $scoreboard players set t1 tbs.threshold_temp $(current)
    $scoreboard players set t2 tbs.threshold_temp $(desired)
    scoreboard players set 360 tbs.threshold_temp 360
    scoreboard players set -1 tbs.threshold_temp -1
    
    scoreboard players operation d tbs.threshold_temp = t1 tbs.threshold_temp
    scoreboard players operation d tbs.threshold_temp -= t2 tbs.threshold_temp
    execute if score d tbs.threshold_temp matches ..-1 run scoreboard players operation d tbs.threshold_temp *= -1 tbs.threshold_temp

    scoreboard players operation d tbs.threshold_temp %= 360 tbs.threshold_temp

    execute if score d tbs.threshold_temp matches 181.. run {
        scoreboard players operation 360 tbs.threshold_temp -= d tbs.threshold_temp
        scoreboard players operation d tbs.threshold_temp = 360 tbs.threshold_temp
    }
    $execute if score d tbs.threshold_temp matches ..$(threshold) run return 1
    return 0
    
}

advancement nullnullnull {
	"display": {
		"background": "minecraft:textures/block/stone.png",
		"icon": {
			"id": "minecraft:acacia_boat",
			"components": {
				"minecraft:item_model": "aaa"
			},
			"count": 1
		},
		"title": "nullnullnull",
		"description": "nullnullnull",
		"frame": "task",
		"show_toast": true,
		"announce_to_chat": true,
		"hidden": false
	},
	"criteria": {
		"nullnullnull_0": {
			"trigger": "minecraft:impossible"
		}
	},
	"requirements": [
		[
			"nullnullnull_0"
		]
	]
}


dir events {


    function 1 {

        schedule 1t {
            execute as @a at @s rotated as @s run {
                        rotate @s 0 0
                        damage @s 1 minecraft:indirect_magic
                    }

            schedule 5t {
                execute as @a at @s rotated as @s run {
                        rotate @s 0 -70
                        damage @s 1 minecraft:indirect_magic
                    }

                    
                schedule 5t {
                    execute as @a at @s rotated as @s run {
                        rotate @s ~65 ~120
                        damage @s 1 minecraft:indirect_magic
                    }


                    schedule 5t {
                        execute as @a at @s rotated as @s run {
                            rotate @s ~-90 ~
                            damage @s 1 minecraft:indirect_magic
                    }
                    }
                }
            }
        }
    }

    function 2 {

        execute as @a run damage @s 1 minecraft:indirect_magic

    }

    function 3 {

        execute as @a at @s run {
            playsound thebrokenscript:randomsong neutral @s ~ ~ ~ 555.0 1.0 0.0
            effect give @s blindness 16 1 false
            effect give @s slow_falling 16 1 false
            effect give @s slowness 16 1 false
            summon lightning_bolt
        }
    }

    function 4 {
        execute as @a[nbt={OnGround:1b}] at @s run {
            summon marker ~ ~ ~ {Tags:["tbs.event.4.fire"]}
            setblock ~ ~ ~ fire
        }

    }

    function 5 {
        execute if predicate {"condition":"minecraft:random_chance","chance":0.5} as @a at @s run {
            execute store result storage tbs:temp event.5.x int 1 run random value -10..10
            execute store result storage tbs:temp event.5.z int 1 run random value -10..10
            block offset_lightning { with storage tbs:temp event.5
                $summon lightning_bolt ~$(x) ~ ~$(z)
            }
        }

    }

    function 6 {
        execute as @a at @s rotated as @s run playsound minecraft:block.grass.break neutral @s ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> 555.0 0.8 0.0


        schedule 20t {
            execute as @a at @s rotated as @s run playsound minecraft:block.grass.break neutral @s ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> 555.0 0.8 0.0


            schedule 20t {
                execute as @a at @s rotated as @s run playsound minecraft:block.grass.break neutral @s ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> 555.0 0.8 0.0

                schedule 20t {
                    execute as @a at @s rotated as @s run playsound minecraft:block.grass.break neutral @s ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> ^<%(Math.random()*10)-5%> 555.0 0.8 0.0
                }
            }
        }

    }

    function 7 {
        function thebrokenscript:night_lib/time_set {time:13000}
        execute if predicate {"condition":"minecraft:random_chance","chance":0.5} as @a at @s run {
            playsound minecraft:ambient.cave neutral @s ~20 ~ ~ 555.0 1.0 0.0
            playsound thebrokenscript:heartbeat neutral @s ~ ~ ~ 555.0 1.0 0.0
        }
    }

    function 8 {
    }

    function 9 {
        kill @r[gamemode=!creative,gamemode=!spectator]
    }

    function 10 {
        execute store result score #event.10.ui tbs.event_temp run random value 1..3
        execute as @a at @s run {
        execute if score #event.10.ui tbs.event_temp matches 1 run function thebrokenscript:overlay_lib/add_overlay {args:{time:100,texture:"thebrokenscript:item/screens/nullinterface1"}}
        execute if score #event.10.ui tbs.event_temp matches 2 run function thebrokenscript:overlay_lib/add_overlay {args:{time:100,texture:"thebrokenscript:item/screens/nullinterface2"}}
        execute if score #event.10.ui tbs.event_temp matches 3 run function thebrokenscript:overlay_lib/add_overlay {args:{time:100,texture:"thebrokenscript:item/screens/nullinterface3"}}

        }
        
    }

    function 11 {
        execute store result score #event.11.random tbs.event_temp run random value 1..13

        execute if score #event.11.random tbs.event_temp matches 1 run tellraw @a "I see you"
        execute if score #event.11.random tbs.event_temp matches 2 run tellraw @a "Can you see me?"
        execute if score #event.11.random tbs.event_temp matches 3 run tellraw @a "I see you"
        execute if score #event.11.random tbs.event_temp matches 4 run tellraw @a "It was your fault."
        execute if score #event.11.random tbs.event_temp matches 5 run tellraw @a "Help us."
        execute if score #event.11.random tbs.event_temp matches 6 run tellraw @a "I am right behind you."
        execute if score #event.11.random tbs.event_temp matches 7 run tellraw @a {"text":"I am right behind you.","color":"dark_red"}
        execute if score #event.11.random tbs.event_temp matches 8 run tellraw @a "null"
        execute if score #event.11.random tbs.event_temp matches 9 run tellraw @a "null.err"
        execute if score #event.11.random tbs.event_temp matches 10 run tellraw @a "000"
        execute if score #event.11.random tbs.event_temp matches 11 run tellraw @a {"text":"AAAAAAAAA","color":"white","obfuscated":true}
        execute if score #event.11.random tbs.event_temp matches 12 run tellraw @a {"text":"Null joined the game","color":"yellow"}
        execute if score #event.11.random tbs.event_temp matches 13 run tellraw @a {"text":"Null left the game","color":"yellow"}
    }

    function 12 {
        execute store result score #event.12.random tbs.event_temp run random value 1..4

        execute if score #event.12.random tbs.event_temp matches 1 as @a at @s run playsound ambient.cave neutral @s ~ ~ ~ 555 1.0 0.0
        execute if score #event.12.random tbs.event_temp matches 2 as @a at @s run playsound music_disc.13 neutral @s ~ ~ ~ 555 1.0 0.0
        execute if score #event.12.random tbs.event_temp matches 3 as @a at @s run playsound music_disc.11 neutral @s ~ ~ ~ 555 1.0 0.0
        execute if score #event.12.random tbs.event_temp matches 4 run stopsound @a
    }

    function 13 {
        advancement revoke @a only thebrokenscript:nullnullnull
        advancement grant @a only thebrokenscript:nullnullnull
    }

    function 14 {
        give @p written_book[written_book_content={pages:["{\"text\":\"null.err.object.err.null.object.alone.3.not.behind.entitytype:player.receiveddata.invalid.reboot.failed.reset.playerdata:00F9219492D94210F812\",\"color\":\"black\"}"],title:"null",author:null}]
    }

    function 15 {
    }

    function 16 {
        title @a times 10 70 20
        title @a title {"text":"null null null null null null null null","color":"white"}
    }

    function 17 {
        execute store result storage tbs:temp event.17.y int 1 run random value 50..60
        block { with storage tbs:temp event.17
            $setblock ~ ~$(y) ~ water
        }
    }

    function 18 {
        execute store result storage tbs:temp event.18.y int 1 run random value 50..60
        block { with storage tbs:temp event.18
            $setblock ~ ~$(y) ~ bedrock
        }
    }

    function 19 {
        execute store result storage tbs:temp event.19.x int 1 run random value -55..55
        execute store result storage tbs:temp event.19.z int 1 run random value -55..55
        execute at @r run block { with storage tbs:temp event.19
            $summon creeper ~$(x) ~ ~$(z) {Fuse:0,ExplosionRadius:-3}
        }
    }

    function 20 {
        execute if predicate {"condition":"minecraft:random_chance","chance":0.7} run {
            function thebrokenscript:night_lib/time_set {time:13000}
        } else run {
            function thebrokenscript:night_lib/time_set {time:0}
        }
    }

    function 21 {
        execute store result storage tbs:temp event.21.x int 1 run random value -5..5
        execute store result storage tbs:temp event.21.y int 1 run random value 0..2
        execute store result storage tbs:temp event.21.z int 1 run random value -5..5
        execute at @r run block { with storage tbs:temp event.21
            $execute positioned ~$(x) ~$(y) ~$(z) run function thebrokenscript:entity/null/scare_summon
        }
    }

    function 22 {
        give @r music_disc_11
    }

    function 23 {
        execute as @a at @s run rotate @s 0 5
    }

    function 24 {
        
    }
}

dir night_lib {

    function main {
        execute store result score day tbs.moon_event run time query day
        execute store result score daytime tbs.moon_event run time query daytime
        scoreboard players operation day tbs.moon_event %= 8 tbs.moon_event
        execute if score enabled tbs.moon_event matches 0 if score day tbs.moon_event matches 4..7 run time add 24000t
        execute if score enabled tbs.moon_event matches 1 if score day tbs.moon_event matches 0 run scoreboard players set enabled tbs.moon_event 0

    }


    function time_set {
    
    $scoreboard players set desired tbs.moon_event $(time)
    scoreboard players set 24000 tbs.moon_event 24000
    execute store result score current_day tbs.moon_event run time query day
    scoreboard players operation current_day tbs.moon_event *= 24000 tbs.moon_event
    scoreboard players operation desired tbs.moon_event += current_day tbs.moon_event
    execute store result storage tbs:temp moon_event.time int 1 run scoreboard players get desired tbs.moon_event
    block time_set_macro { with storage tbs:temp moon_event
        $time set $(time)
    }
    
    }

}

dir overlay_lib {

    loot_table player_head {
	    "type": "minecraft:empty",
	    "pools": [
	    	{
	    		"rolls": 1,
	    		"entries": [
	    			{
	    				"type": "minecraft:item",
	    				"name": "minecraft:player_head",
	    				"functions": [
	    					{
	    						"function": "minecraft:fill_player_head",
	    						"entity": "this"
	    					}
	    				]
	    			}
	    		]
	    	}
	    ]
    }

    predicate head {
	    "condition": "minecraft:entity_properties",
	    "entity": "this",
	    "predicate": {
	    	"equipment": {
	    		"head": {
	    			"components": {
	    				"minecraft:custom_data": {
	    					"tbs.overlay_lib.overlay": true
	    				}
	    			}
	    		}
	    	}
	    }
    }

    predicate chest {
	    "condition": "minecraft:entity_properties",
	    "entity": "this",
	    "predicate": {
	    	"equipment": {
	    		"chest": {
	    			"components": {
	    				"minecraft:custom_data": {
	    					"tbs.overlay_lib.overlay": true
	    				}
	    			}
	    		}
	    	}
	    }
    }

    predicate legs {
	    "condition": "minecraft:entity_properties",
	    "entity": "this",
	    "predicate": {
	    	"equipment": {
	    		"legs": {
	    			"components": {
	    				"minecraft:custom_data": {
	    					"tbs.overlay_lib.overlay": true
	    				}
	    			}
	    		}
	    	}
	    }
    }

    predicate feet {
	    "condition": "minecraft:entity_properties",
	    "entity": "this",
	    "predicate": {
	    	"equipment": {
	    		"feet": {
	    			"components": {
	    				"minecraft:custom_data": {
	    					"tbs.overlay_lib.overlay": true
	    				}
	    			}
	    		}
	    	}
	    }
    }

    predicate any_of {
	    "condition": "minecraft:any_of",
	    "terms": [
	    	{
	    		"condition": "minecraft:entity_properties",
	    		"entity": "this",
	    		"predicate": {
	    			"equipment": {
	    				"head": {
	    					"components": {
	    						"minecraft:custom_data": {
	    							"tbs.overlay_lib.overlay": true
	    						}
	    					}
	    				}
	    			}
	    		}
	    	},
	    	{
	    		"condition": "minecraft:entity_properties",
	    		"entity": "this",
	    		"predicate": {
	    			"equipment": {
	    				"chest": {
	    					"components": {
	    						"minecraft:custom_data": {
	    							"tbs.overlay_lib.overlay": true
	    						}
	    					}
	    				}
	    			}
	    		}
	    	},
	    	{
	    		"condition": "minecraft:entity_properties",
	    		"entity": "this",
	    		"predicate": {
	    			"equipment": {
	    				"legs": {
	    					"components": {
	    						"minecraft:custom_data": {
	    							"tbs.overlay_lib.overlay": true
	    						}
	    					}
	    				}
	    			}
	    		}
	    	},
	    	{
	    		"condition": "minecraft:entity_properties",
	    		"entity": "this",
	    		"predicate": {
	    			"equipment": {
	    				"feet": {
	    					"components": {
	    						"minecraft:custom_data": {
	    							"tbs.overlay_lib.overlay": true
	    						}
	    					}
	    				}
	    			}
	    		}
	    	}
	    ]
    }

    function get_name {
        summon item_display ~ ~ ~ {Tags:["tbs.overlay_lib.player_name"]}
        loot replace entity @n[tag=tbs.overlay_lib.player_name] contents loot thebrokenscript:overlay_lib/player_head
        $data modify storage $(storage) $(path) set from entity @n[tag=tbs.overlay_lib.player_name] item.components."minecraft:profile".name
        kill @n[tag=tbs.overlay_lib.player_name]
    }
    
    function as_player {
        execute as @a[predicate=thebrokenscript:overlay_lib/any_of] at @s rotated as @s run {
            function thebrokenscript:overlay_lib/get_name {storage:"tbs:temp",path:"overlay.username"}
            block update { with storage tbs:temp overlay

            $execute store result score count tbs.overlay_temp run data get storage tbs:temp overlay.players[{username:"$(username)"}].active
            
            
            REPEAT (0,3) as i {
                execute if score count tbs.overlay_temp matches <%i+1%>.. run {
                data modify storage tbs:temp overlay.slot set value <%i%>
                function thebrokenscript:overlay_lib/as_slot with storage tbs:temp overlay
                }
            }
            
            }



        }
    }
    
    function as_slot {
        $tellraw @s[tag=tbs.dev] ["",{"nbt":"overlay.players[{username:\"$(username)\"}].active[$(slot)].time","storage":"tbs:temp"},{"text":" __ "},{"nbt":"overlay.players[{username:\"$(username)\"}].active[$(slot)].image","storage":"tbs:temp"}]
        # $data modify storage tbs:temp overlay.current set from storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot)]
        $data modify storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot)].slot_num set value $(slot)
        # $data modify storage tbs:temp overlay.current.username set value "$(username)"

        $execute store result score timer tbs.overlay_temp run data get storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot)].time
        execute if score timer tbs.overlay_temp matches ..0 run block { with storage tbs:temp overlay
            $data modify storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot)].username set value "$(username)"
            $function thebrokenscript:overlay_lib/remove_slot with storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot)]
        }
        $execute unless score timer tbs.overlay_temp matches ..0 store result storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot)].time int 1 run scoreboard players remove timer tbs.overlay_temp 1
    }

    function remove_slot {
        # armor.head
        # armor.chest
        # armor.legs
        # armor.feet
        $execute if predicate thebrokenscript:overlay_lib/$(slot) run item replace entity @s armor.$(slot) with air
        $data remove storage tbs:temp overlay.players[{username:"$(username)"}].active[$(slot_num)]
    }

    function add_overlay {
        # args:
        #
        # time: int (ticks)
        # texture: str (texture path, from assets/<namespace>/textures/<id>)
        $data modify storage tbs:temp overlay.add set value $(args)
        function thebrokenscript:overlay_lib/get_name {storage:"tbs:temp",path:"overlay.add.username"}
        function thebrokenscript:overlay_lib/generate_storage with storage tbs:temp overlay.add

        execute store result score available_slot tbs.overlay_temp run {
            execute unless predicate thebrokenscript:overlay_lib/head run return 1
            execute unless predicate thebrokenscript:overlay_lib/chest run return 2
            execute unless predicate thebrokenscript:overlay_lib/legs run return 3
            execute unless predicate thebrokenscript:overlay_lib/feet run return 4
            return 0
        }
        execute if score available_slot tbs.overlay_temp matches 1 run data modify storage tbs:temp overlay.add.slot set value "head"
        execute if score available_slot tbs.overlay_temp matches 2 run data modify storage tbs:temp overlay.add.slot set value "chest"
        execute if score available_slot tbs.overlay_temp matches 3 run data modify storage tbs:temp overlay.add.slot set value "legs"
        execute if score available_slot tbs.overlay_temp matches 4 run data modify storage tbs:temp overlay.add.slot set value "feet"



        execute if score available_slot tbs.overlay_temp matches 0 run return 0
        block equip_overlay { with storage tbs:temp overlay.add

            $execute if items entity @s armor.$(slot) * run { with storage tbs:temp overlay.add
                summon item ~ ~ ~ {Item:{id:"minecraft:stick",count:1,components:{item_model:"minecraft:air"}},PickupDelay:0s,Tags:["tbs.overlay_lib.equip_item"]}
                data modify entity @n[tag=tbs.overlay_lib.equip_item] Owner set from entity @s UUID
                $item replace entity @n[tag=tbs.overlay_lib.equip_item] contents from entity @s armor.$(slot)
                $item replace entity @s armor.$(slot) with air
            }

            $item replace entity @s armor.$(slot) with minecraft:beetroot_soup[\
            minecraft:equippable={slot:"$(slot)",camera_overlay:"$(texture)",asset_id:"minecraft:empty",equip_sound:{sound_id:"minecraft:block.glass.break",range:0}},\
            minecraft:item_model="minecraft:air",\
            minecraft:hide_tooltip={},\
            minecraft:hide_additional_tooltip={},\
            enchantments={binding_curse:1,vanishing_curse:1},\
            enchantment_glint_override=false,\
            custom_data={tbs.overlay_lib.overlay:1b}\
            ]

            
            $data modify storage tbs:temp overlay.players[{username:"$(username)"}].active append value {slot:"$(slot)",image:"$(texture)",time:$(time)}
        }
    }
    function generate_storage {
        $execute if data storage tbs:temp overlay.players[{username:"$(username)"}] run return 0
        $data append storage tbs:temp overlay.players append value {username:"$(username)",active:[]}
    }
}

dir entity {
    function player {
        execute as @e[tag=aj.tbs_player.root] at @s rotated as @s run block {
            execute on passengers if entity @s[tag=aj.tbs_player.data] run data modify storage tbs:temp player.head set from entity @s data.locators.head.uuid
            execute on passengers if entity @s[tag=aj.tbs_player.bone.head1] run block {
                data modify storage tbs:temp player.variant set from entity @s item.components."minecraft:custom_model_data".strings[0]
                block fill_player_head { with storage tbs:temp player

                    $data modify entity $(head) item.components."minecraft:item_model" set value "thebrokenscript:heads/$(variant)"
                }
            }
            block rotate_body { with storage tbs:temp player
                    $execute store result storage tbs:temp entity_rotation.desired int 1 run data get entity $(head) Rotation[0]
                    execute store result storage tbs:temp entity_rotation.current int 1 run data get entity @s Rotation[0]
                    data modify storage tbs:temp entity_rotation.threshold set value 30
                    execute store result score result tbs.entity_temp run function thebrokenscript:rotation_treshold with storage tbs:temp entity_rotation
                    # $execute unless score result tbs.entity_temp matches 1 rotated as $(head) run rotate @s ~ 0
                    $execute unless score result tbs.entity_temp matches 1 at @s rotated as $(head) positioned ^ ^ ^5 rotated as @s positioned ^ ^ ^40 facing entity @s eyes facing ^ ^ ^-1 positioned as @s run rotate @s ~ 0
            }
            # item.components."minecraft:custom_model_data".strings[0]
        }
    }
    function rotate_player {
        execute on passengers if entity @s[tag=aj.tbs_player.data] run data modify storage tbs:temp player.head set from entity @s data.locators.head.uuid
        block { with storage tbs:temp player
            $rotate $(head) facing entity @p eyes
        }
    }
    dir null {
        function scare_summon  {
            execute rotated ~ 0 run function animated_java:tbs_player/summon {args:{animation:'idle',start_animation:true,variant:'null'}}
            execute as @n[tag=!tbs.entity,tag=aj.tbs_player.root] at @s run function thebrokenscript:entity/set_procedures {args:{spawn:"null_scare_spawn",tick:"null_scare_tick",name:"null_scare"}}

        }
        function endgame_summon {

        }

    }

    function set_procedures {
        # args:
        #
        #   tick: str, function
        #   spawn: str, function
        #   kill: str, function
        #
        #   name: str, entity_name (tagged)
        #
        execute unless data storage tbs:entity entities run data modify storage tbs:entity entities set value []
        tag @s add tbs.entity
        # tick, spawn, kill (kills player)
        function thebrokenscript:uuid_to_string/init with entity @s
        # outputs to tbs:uuid main.uuid
        $data modify storage tbs:temp entity.set_procedures.input set value $(args)
        REPEAT (['tick','spawn','kill','name']) as key {
            data modify storage tbs:temp entity.set_procedures.<%key%> set value "none"
            data modify storage tbs:temp entity.set_procedures.<%key%> set from storage tbs:temp entity.set_procedures.input.<%key%>
        }
        
        data modify storage tbs:temp entity.set_procedures.uuid set from storage tbs:uuid main.uuid
        block { with storage tbs:temp entity.set_procedures
            $tag @s add tbs.$(name)
            $data modify storage tbs:entity entities append value {entity:"$(uuid)"}
            $data modify storage tbs:entity entities.[{entity:"$(uuid)"}] set value {entity:"$(uuid)",tick:"$(tick)",spawn:"$(spawn)",kill:"$(kill)"}
        }
    }

    dir entity_procedures {
        function none {
        }

        function null_scare_spawn {
            execute if predicate {"condition":"minecraft:random_chance","chance":0.9} run {
                playsound thebrokenscript:nullkillsplayer neutral @a ~ ~ ~ 555.0 1.0 0.0
                execute as @a[distance=0..10] run function thebrokenscript:damage_macro {name:'Null',damage:5}
                } else run {
                    function thebrokenscript:entity/null/endgame_summon
                    say endgame
                }
        }

        function null_scare_tick {
                scoreboard players add @s tbs.entity_temp 1
                execute if score @s tbs.entity_temp matches 10.. run function thebrokenscript:entity/remove {command:"function animated_java:tbs_player/remove/this"}
                function thebrokenscript:entity/rotate_player

        }
    }
    function remove {
        # args:
        # command: str, function that normally removes entity
        function thebrokenscript:uuid_to_string/init with entity @s
        block { with storage tbs:temp main
        $data remove storage tbs:entity entities[{entity:"$(uuid)"}]
        }
        $$(command)

    }
    function entity_tick {
        function thebrokenscript:uuid_to_string/init with entity @s
        block { with storage tbs:uuid main
            $block { with storage tbs:entity entities[{entity:"$(uuid)"}]
                $function thebrokenscript:entity/entity_procedures/$(spawn)
                $data modify storage tbs:entity entities[{entity:"$(entity)"}].spawn set value "none"
                $function thebrokenscript:entity/entity_procedures/$(tick)
                $execute if entity @s[tag=tbs.entity.killed_player] run function thebrokenscript:entity/entity_procedures/$(kill)
                execute if entity @s[tag=tbs.entity.killed_player] run tag @s remove tbs.entity.killed_player
                }
        }
    }
}

dir uuid_to_string {
    function init {
        # uuid to string from animated java
        scoreboard players set 256 tbs.entity_temp 256
        data modify storage tbs:uuid main.hex_chars set value ["00","01","02","03","04","05","06","07","08","09","0a","0b","0c","0d","0e","0f","10","11","12","13","14","15","16","17","18","19","1a","1b","1c","1d","1e","1f","20","21","22","23","24","25","26","27","28","29","2a","2b","2c","2d","2e","2f","30","31","32","33","34","35","36","37","38","39","3a","3b","3c","3d","3e","3f","40","41","42","43","44","45","46","47","48","49","4a","4b","4c","4d","4e","4f","50","51","52","53","54","55","56","57","58","59","5a","5b","5c","5d","5e","5f","60","61","62","63","64","65","66","67","68","69","6a","6b","6c","6d","6e","6f","70","71","72","73","74","75","76","77","78","79","7a","7b","7c","7d","7e","7f","80","81","82","83","84","85","86","87","88","89","8a","8b","8c","8d","8e","8f","90","91","92","93","94","95","96","97","98","99","9a","9b","9c","9d","9e","9f","a0","a1","a2","a3","a4","a5","a6","a7","a8","a9","aa","ab","ac","ad","ae","af","b0","b1","b2","b3","b4","b5","b6","b7","b8","b9","ba","bb","bc","bd","be","bf","c0","c1","c2","c3","c4","c5","c6","c7","c8","c9","ca","cb","cc","cd","ce","cf","d0","d1","d2","d3","d4","d5","d6","d7","d8","d9","da","db","dc","dd","de","df","e0","e1","e2","e3","e4","e5","e6","e7","e8","e9","ea","eb","ec","ed","ee","ef","f0","f1","f2","f3","f4","f5","f6","f7","f8","f9","fa","fb","fc","fd","fe","ff"]

        data modify storage tbs:uuid temp set value {0:0,1:0,2:0,3:0,4:0,5:0,6:0,7:0,8:0,9:0,a:0,b:0,c:0,d:0,e:0,f:0}
        $data modify storage tbs:uuid main.in set value $(UUID)
        execute store result score 0= tbs.entity_temp store result score 1= tbs.entity_temp run data get storage tbs:uuid main.in[0]
        execute store result storage tbs:uuid temp.0 int 1 run scoreboard players operation 0= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 2= tbs.entity_temp run scoreboard players operation 1= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.1 int 1 run scoreboard players operation 1= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 3= tbs.entity_temp run scoreboard players operation 2= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.2 int 1 run scoreboard players operation 2= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.3 int 1 run scoreboard players operation 3= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result score 0= tbs.entity_temp store result score 1= tbs.entity_temp run data get storage tbs:uuid main.in[1]
        execute store result storage tbs:uuid temp.4 int 1 run scoreboard players operation 0= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 2= tbs.entity_temp run scoreboard players operation 1= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.5 int 1 run scoreboard players operation 1= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 3= tbs.entity_temp run scoreboard players operation 2= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.6 int 1 run scoreboard players operation 2= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.7 int 1 run scoreboard players operation 3= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result score 0= tbs.entity_temp store result score 1= tbs.entity_temp run data get storage tbs:uuid main.in[2]
        execute store result storage tbs:uuid temp.8 int 1 run scoreboard players operation 0= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 2= tbs.entity_temp run scoreboard players operation 1= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.9 int 1 run scoreboard players operation 1= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 3= tbs.entity_temp run scoreboard players operation 2= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.a int 1 run scoreboard players operation 2= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.b int 1 run scoreboard players operation 3= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result score 0= tbs.entity_temp store result score 1= tbs.entity_temp run data get storage tbs:uuid main.in[3]
        execute store result storage tbs:uuid temp.c int 1 run scoreboard players operation 0= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 2= tbs.entity_temp run scoreboard players operation 1= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.d int 1 run scoreboard players operation 1= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result score 3= tbs.entity_temp run scoreboard players operation 2= tbs.entity_temp /= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.e int 1 run scoreboard players operation 2= tbs.entity_temp %= 256 tbs.entity_temp
        execute store result storage tbs:uuid temp.f int 1 run scoreboard players operation 3= tbs.entity_temp /= 256 tbs.entity_temp
        block { with storage tbs:uuid temp
            $data modify storage tbs:uuid temp.0 set from storage tbs:uuid main.hex_chars[$(0)]
            $data modify storage tbs:uuid temp.1 set from storage tbs:uuid main.hex_chars[$(1)]
            $data modify storage tbs:uuid temp.2 set from storage tbs:uuid main.hex_chars[$(2)]
            $data modify storage tbs:uuid temp.3 set from storage tbs:uuid main.hex_chars[$(3)]
            $data modify storage tbs:uuid temp.4 set from storage tbs:uuid main.hex_chars[$(4)]
            $data modify storage tbs:uuid temp.5 set from storage tbs:uuid main.hex_chars[$(5)]
            $data modify storage tbs:uuid temp.6 set from storage tbs:uuid main.hex_chars[$(6)]
            $data modify storage tbs:uuid temp.7 set from storage tbs:uuid main.hex_chars[$(7)]
            $data modify storage tbs:uuid temp.8 set from storage tbs:uuid main.hex_chars[$(8)]
            $data modify storage tbs:uuid temp.9 set from storage tbs:uuid main.hex_chars[$(9)]
            $data modify storage tbs:uuid temp.a set from storage tbs:uuid main.hex_chars[$(a)]
            $data modify storage tbs:uuid temp.b set from storage tbs:uuid main.hex_chars[$(b)]
            $data modify storage tbs:uuid temp.c set from storage tbs:uuid main.hex_chars[$(c)]
            $data modify storage tbs:uuid temp.d set from storage tbs:uuid main.hex_chars[$(d)]
            $data modify storage tbs:uuid temp.e set from storage tbs:uuid main.hex_chars[$(e)]
            $data modify storage tbs:uuid temp.f set from storage tbs:uuid main.hex_chars[$(f)]
        }
        block { with storage tbs:uuid temp
            $data modify storage tbs:uuid main.uuid set value "$(3)$(2)$(1)$(0)-$(7)$(6)-$(5)$(4)-$(b)$(a)-$(9)$(8)$(f)$(e)$(d)$(c)"
        }

    }
}